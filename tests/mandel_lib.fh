
fn calc_point(cx, cy, max_iter)
{
    let i = 0;
    let x = 0;
    let y = 0;
    while (i < max_iter) {
        let t = x*x - y*y + cx;
        y = 2*x*y + cy;
        x = t;
        if (x*x + y*y > 4)
            break;
        i = i + 1;
    }
    return i;
}

fn mandelbrot(x1,y1, x2,y2, size_x,size_y, max_iter, out)
{
    let step_x = (x2-x1)/(size_x-1);
    let step_y = (y2-y1)/(size_y-1);

    let y = y1;
    let ny = 0;
    while (ny < size_y) {
        let x = x1;
        let nx = 0;
        while (nx < size_x) {
            let c = calc_point(x, y, max_iter);
            if (c == max_iter)
                out.point_inside();
            else
                out.point_outside(c);
            x = x + step_x;
            nx = nx + 1;
        }
        y = y + step_y;
        ny = ny + 1;
        out.end_line();
    }
}
